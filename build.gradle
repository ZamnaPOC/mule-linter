plugins {
    id 'groovy'
    id 'java'
    id 'application'
    id 'codenarc'
    id 'maven-publish'
    id 'org.hibernate.build.maven-repo-auth' version '3.0.3'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'com.avioconsulting.mule'
mainClassName = 'com.avioconsulting.mule.MuleLinterCli'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.0'
    implementation 'info.picocli:picocli-groovy:4.3.2'
    testImplementation platform('org.spockframework:spock-bom:2.0-M3-groovy-3.0')
    testImplementation 'org.spockframework:spock-core'
}

test {
    useJUnitPlatform()
}

// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.CodeNarcExtension.html
codenarc {
    configFile = file("${project.projectDir}/config/code-quality-config/codenarc/codenarc.xml")
    ignoreFailures = true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv("MVN_USER") != null ? System.getenv("MVN_USER") : ''
                password = System.getenv("MVN_PASSWORD") != null ? System.getenv("MVN_PASSWORD") : ''
            }
            def repo = "https://devops.avioconsulting.com/nexus/repository/avio-"
            url = version.endsWith('SNAPSHOT') ? "${repo}snapshots" : "${repo}releases"
        }
    }
}